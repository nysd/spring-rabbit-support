buildscript {
  repositories { mavenCentral() }
  ext {
    springBootVersion = '1.3.5.RELEASE'
    springVersion = '4.2.5.RELEASE'
    springRabbitVersion = '1.5.4.RELEASE'
    checkstyleVersion = '6.19'
    findbugsVersion = '3.0.1'
  }
  dependencies { classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

group = 'spring.support'
version = '1.0.0'

description = """"""

// javacオプション
compileJava { options.encoding = 'UTF-8' }
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenLocal()
  maven { url "http://${nexusHost}/nexus/content/repositories/spr" }
  mavenCentral()
  jcenter()
}

findbugs {
  toolVersion = "${findbugsVersion}"
  effort = "default" // min,default,max
  reportLevel = "high" // low,medium,high
  ignoreFailures = true
}

dependencies {
  compile group: 'org.springframework.amqp', name: 'spring-rabbit', version:"${springRabbitVersion}"
  compile group: 'org.springframework', name: 'spring-jdbc', version:"${springVersion}"
  compile group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version:"${springBootVersion}"
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version:"${springBootVersion}"
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-logging', version:"${springBootVersion}"
  testCompile group: 'org.springframework', name: 'spring-test', version:"${springVersion}"
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version:"${springBootVersion}"
  testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter', version:"${springBootVersion}") { exclude(module: 'commons-logging') }
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version:"${springBootVersion}"
  testCompile group: 'com.googlecode.log4jdbc', name: 'log4jdbc', version:'1.2'
  testCompile group: 'junit', name: 'junit', version:'4.12'
  testCompile group: 'org.projectlombok', name: 'lombok', version:'1.16.6'
  testCompile group: 'oracle.jdbc', name: 'ojdbc7', version:'12.1.0.2.0'
  checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleVersion}"
  findbugs "com.google.code.findbugs:findbugs:${findbugsVersion}"
}

// Mavenリポジトリへ配置する対象にソースとJavaDocのJARも含める
task sourcesJar(type: Jar, dependsOn:classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

// publish
publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId 'spring-rabbit-support'

      from components.java

      artifact sourcesJar { classifier "sources" }
    }
  }
  repositories {
    maven {
      if(project.version.endsWith('-SNAPSHOT')) {
        url "http://${nexusHost}/nexus/content/repositories/spr-snapshots/"
      } else {
        url "http://${nexusHost}/nexus/content/repositories/spr-releases/"
      }
    }
  }
}
